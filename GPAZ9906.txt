      *---------------------------------------------------------------*
      *OBJETIVO DO PROGRAMA: ATUALIZAR O ARQUIVO QUE CONTEM CLIENTES  *
      *                      DEVEDORES COM BASE NA MOVIMENTACAO DO    *
      *                      PERIODO, GERANDO UM CADASTRO DE CLIENTES *
      *                      DEVEDORES ATUALIZADO                     *
      *---------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *---------------------------------------------------------------*
       PROGRAM-ID.                         GPAZ9906.
       AUTHOR.                             KETRUS.
       DATE-WRITTEN.                       07/06/2021.
       DATE-COMPILED.                      09/06/2021.
       SECURITY.                           NENHUM.
      *---------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *---------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SOURCE-COMPUTER.                    NOTEMIRLA.
       OBJECT-COMPUTER.                    NOTEMIRLA.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT                        SECTION.

      *ETAPA 1 : ESPECIFICACAO DOS ARQUIVOS DE ENTRADA E SAIDA

       FILE-CONTROL.
           SELECT CLIOLD ASSIGN            TO UT-S-CLIOLD
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS FS-CLIOLD
           .

           SELECT CLIMOV ASSIGN            TO UT-S-CLIMOV
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS FS-CLIMOV
           .

           SELECT CLINEW  ASSIGN           TO UT-S-CLINEW
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS FS-CLINEW
           .

      *---------------------------------------------------------------*
       DATA                                DIVISION.
      *---------------------------------------------------------------*
       FILE                                SECTION.
      *---------------------------------------------------------------*
      *ETAPA 2: DETALHAMENTO DOS ARQUIVOS DE ENTRADA E SAIDA

      *VARIAVEIS ORIGINAIS CLIOLD

       FD  CLIOLD
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 79 CHARACTERS
           DATA RECORD IS REG-CLIOLD
           .

      *VARIAVEIS ORIGINAIS CLIOLD VINDAS DA BOOKLIB

           COPY ARQCLINO REPLACING ==:XX-:== BY ====
                                   ==:YY:== BY ==O==.

      *VARIAVEIS ORIGINAIS CLIMOV

       FD  CLIMOV
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           DATA RECORD IS REG-CLIMOV
           .

      *VARIAVEIS ORIGINAIS CLIMOV VINDAS DA BOOKLIB

           COPY ARQCLIM  REPLACING ==:XX-:== BY ====
                                   ==:YY:== BY ==M==.


      *VARIAVEIS ORIGINAIS CLINEW

       FD  CLINEW
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 79 CHARACTERS
           DATA RECORD IS REG-CLINEW
           .

      *VARIAVEIS ORIGINAIS CLINEW VINDAS DA BOOKLIB

           COPY ARQCLINO REPLACING ==:XX-:== BY ====
                                   ==:YY:== BY ==N==.

      *---------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *---------------------------------------------------------------*
      *VARIAVEIS DE FILE STATUS COM FLAGS

       01  FS-CLIOLD                       PIC X(02).
           88 SUCESSO-O                            VALUE '00'.
           88 FIM-ARQ-O                            VALUE '10'.

       01  FS-CLIMOV                       PIC X(02).
           88 SUCESSO-M                            VALUE '00'.
           88 FIM-ARQ-M                            VALUE '10'.

       01  FS-CLINEW                       PIC X(02).
           88 SUCESSO-N                            VALUE '00'.
           88 FIM-ARQ-N                            VALUE '10'.

      *VARIAVEIS ESPELHO CLIOLD VINDAS DA BOOKLIB

       COPY ARQCLINO REPLACING ==:XX-:== BY ==WS-==
                               ==:YY:== BY ==O==.

      *VARIAVEIS ESPELHO CLIMOV VINDAS DA BOOKLIB

       COPY ARQCLIM  REPLACING ==:XX-:== BY ==WS-==
                               ==:YY:== BY ==M==.

      *VARIAVEIS ESPELHO CLINEW VINDAS DA BOOKLIB

       COPY ARQCLINO REPLACING ==:XX-:== BY ==WS-==
                               ==:YY:== BY ==N==.

      *VARIAVEIS DE DATA VINDAS DA BOOKLIB

       COPY VARDATA.

      *VARIAVEIS DE TEMPO VINDAS DA BOOKLIB

       COPY VARTEMP.

      *VARIAVES DE MENSAGENS

       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).

      *VARIAVEIS DE CONTADORES DE REGISTROS LIDOS E GRAVADOS
      *TIPO COMP

       77  WS-CTLIDO-CLIOLD                PIC 9(02) COMP.
       77  WS-CTLIDO-CLIMOV                PIC 9(02) COMP.
       77  WS-CTGRAV-CLINEW                PIC 9(02) COMP.

      *VARIAVEIS DE CONTADORES INCLUIDOS

       77  WS-CTINC                        PIC 9(02) COMP.

      *VARIAVEIS DE CONTADORES EXCLUIDOS

       77  WS-CTEXC                        PIC 9(02) COMP.

      *VARIAVEIS DE CONTADORES ALTERADOS

       77  WS-CTALT                        PIC 9(02) COMP.

      *VARIAVES DE CONTADORES DE CADASTROS INVALIDOS

       77  WS-CTCADINV                     PIC 9(02) COMP.

      *VARIAVEIS DE CONTADORES DE MOVIMENTOS INVALIDOS

       77  WS-CTMOVINV                     PIC 9(02) COMP.

      *VARIAVEIS DE CONTADORES DE REGISTROS LIDOS E GRAVADOS
      *COM MASCARA

       77  WS-CTLIDO-CLIOLD-F              PIC ZZZ9.
       77  WS-CTLIDO-CLIMOV-F              PIC ZZZ9.
       77  WS-CTGRAV-CLINEW-F              PIC ZZZ9.

      *VARIAVEL DE INCLUSAO COM MASCARA

       77  WS-CTINC-F                      PIC ZZZ9.

      *VARIAVEL DE EXCLUSAO COM MASCARA

       77  WS-CTEXC-F                      PIC ZZZ9.

      *VARIAVEL DE ALTERACAO COM MASCARA

       77  WS-CTALT-F                      PIC ZZZ9.

      *VARIAVEL DE CADASTRO INVALIDO COM MASCARA

       77  WS-CTCADINV-F                   PIC ZZZ9.

      *VARIAVEL DE MOVIMENTO INVALIDO COM MASCARA

       77  WS-CTMOVINV-F                   PIC ZZZ9.

      *VARIAVEL DE NOME DO PROGRAMA VERIFICADOR

       77  WS-NOME-PGM                     PIC X(08)
                                                   VALUE 'PGMAUX02'.

      *VARIAVEIS DE TROCA ENTRE PGM E VERIFICADOR DE CPF

       01  WS-DADOS-ENVIADOS.
           05 WS-TOTALDIVIDA               PIC 9(08)V99.
           05 WS-RESP                      PIC X(01).
           05 WS-DIVIDACALC                PIC 9(08)V99.


      *------------------------------------------------------------*
       LINKAGE                             SECTION.
      *------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *------------------------------------------------------------*
      *ETAPA 3: MANIPULACAO DOS ARQUIVOS DE ENTRADA E SAIDA
       0000-GPAZ9906.
           PERFORM 1000-INICIALIZAR.
           PERFORM 2000-PROCESSAR UNTIL FS-CLIOLD = '10' AND
                                        FS-CLIMOV = '10'
           PERFORM 3000-TERMINO.
           STOP RUN
           .

       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIO FROM TIME
           MOVE 0                          TO WS-CTLIDO-CLIOLD
                                              WS-CTLIDO-CLIMOV
                                              WS-CTGRAV-CLINEW
                                              WS-CTINC
                                              WS-CTEXC
                                              WS-CTALT
                                              WS-CTCADINV
                                              WS-CTMOVINV


           OPEN INPUT CLIOLD
           IF NOT SUCESSO-O

              MOVE 'ERRO NO ARQUIVO DE ABERTURA CLIOLD'
                                           TO WS-MSG
              MOVE FS-CLIOLD               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
           OPEN INPUT CLIMOV

           IF NOT SUCESSO-M

              MOVE 'ERRO DE ABERTURA ARQUIVO DO CLIMOV'
                                           TO WS-MSG
              MOVE FS-CLIMOV               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
           OPEN OUTPUT CLINEW

           IF NOT SUCESSO-N

              MOVE 'ERRO DE ABERTURA DE ARQUIVO DO CLINEW'
                                           TO WS-MSG
              MOVE  FS-CLINEW              TO WS-FS
              GO                           TO 9999-ERRO
           END-IF

           PERFORM 1100-LER-CLIOLD
           PERFORM 1200-LER-CLIMOV
           .
       1100-LER-CLIOLD.
           READ CLIOLD INTO WS-REG-CLI-O
           IF SUCESSO-O
              ADD 1                        TO WS-CTLIDO-CLIOLD
           ELSE
              IF FIM-ARQ-O
                 MOVE HIGH-VALUES          TO WS-CODCLI-O
              ELSE
                 MOVE 'ERRO LEITURA DO ARQUIVO CLIOLD'
                                           TO WS-MSG
                 MOVE FS-CLIOLD            TO WS-FS
                 GO                        TO 9999-ERRO
              END-IF
           END-IF
           .
       1200-LER-CLIMOV.
           READ CLIMOV INTO WS-REG-CLI-M
           IF SUCESSO-M
              ADD 1                        TO WS-CTLIDO-CLIMOV
           ELSE
              IF FIM-ARQ-M
                 MOVE HIGH-VALUES          TO WS-CODCLI-M
              ELSE
                 MOVE 'ERRO LEITURA DO ARQUIVO CLIMOV'
                                           TO WS-MSG
                 MOVE FS-CLIMOV            TO WS-FS
                 GO                        TO 9999-ERRO
              END-IF
           END-IF
           .
       2000-PROCESSAR.
           IF WS-CODCLI-O < WS-CODCLI-M
              ADD 1                        TO WS-CTCADINV
              PERFORM 1100-LER-CLIOLD
           ELSE
              IF WS-CODCLI-O > WS-CODCLI-M
                 PERFORM 2100-INCLUIR
                 PERFORM 1200-LER-CLIMOV
              ELSE
                 PERFORM 2200-ALT-EXC
                 PERFORM 1100-LER-CLIOLD
                 PERFORM 1200-LER-CLIMOV
              END-IF
           END-IF
           .
       2100-INCLUIR.
           IF WS-TIPOMOV-M = 'I'
              PERFORM 2400-GRAVA-M
              ADD 1                        TO WS-CTINC
           ELSE
              ADD 1                        TO WS-CTMOVINV
           END-IF
           .
       2200-ALT-EXC.
           IF WS-TIPOMOV-M = 'A'
              PERFORM 2300-ALTERAR
           ELSE
              IF WS-TIPOMOV-M = 'E'
                 ADD 1                     TO WS-CTEXC
              ELSE
                 ADD 1                     TO WS-CTMOVINV
              END-IF
           END-IF
           .
       2300-ALTERAR.
           MOVE WS-TOTALDIVIDA-O           TO WS-TOTALDIVIDA

           MOVE SPACES                     TO WS-RESP
           CALL WS-NOME-PGM USING WS-DADOS-ENVIADOS
           CANCEL WS-NOME-PGM
           IF WS-RESP = '1'
              MOVE WS-DIVIDACALC           TO WS-TOTALDIVIDA-M
              ADD 1                        TO WS-CTALT
              PERFORM 2400-GRAVA-M
              MOVE SPACE                   TO WS-RESP
           ELSE
              ADD 1                        TO WS-CTMOVINV
              ADD 1                        TO WS-CTCADINV
           END-IF
           .
       2400-GRAVA-M.
           MOVE WS-CODCLI-M                TO WS-CODCLI-N
           IF WS-NOMECLI-M NOT = SPACE
              MOVE WS-NOMECLI-M            TO WS-NOMECLI-N
           ELSE
              MOVE WS-NOMECLI-O            TO WS-NOMECLI-N
           END-IF
           IF WS-ENDCLI-M  NOT = SPACE
              MOVE WS-ENDCLI-M             TO WS-ENDCLI-N
           ELSE
              MOVE WS-ENDCLI-O             TO WS-ENDCLI-N
           END-IF
           IF WS-FONECLI-M NOT = SPACE
              MOVE WS-FONECLI-M            TO WS-FONECLI-N
           ELSE
              MOVE WS-FONECLI-O            TO WS-FONECLI-N
           END-IF
           IF WS-TOTALDIVIDA-M IS NUMERIC
              MOVE WS-TOTALDIVIDA-M        TO WS-TOTALDIVIDA-N
           ELSE
              IF WS-RESP NOT = '0'

                 MOVE WS-TOTALDIVIDA-M     TO WS-TOTALDIVIDA-N
              END-IF
           END-IF

           WRITE REG-CLI-N FROM WS-REG-CLI-N

           IF NOT SUCESSO-N

              MOVE 'ERRO DE GRAVACAO NO ARQ CLINEW'
                                           TO WS-MSG
              MOVE FS-CLINEW               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
           ADD 1                           TO WS-CTGRAV-CLINEW
           .
       3000-TERMINO.
           PERFORM 9000-IMPRIME-DATA
           CLOSE CLIOLD
           IF NOT SUCESSO-O

              MOVE 'ERRO DE FECHAMENTO DO ARQUIVO CLIOLD'
                                           TO WS-MSG
              MOVE FS-CLIOLD               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF

           CLOSE CLIMOV
           IF NOT SUCESSO-M

              MOVE 'ERRO DE FECHAMENTO DO ARQUIVO CLIMOV'
                                           TO WS-MSG
              MOVE FS-CLIMOV               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF

           CLOSE CLINEW
           IF NOT SUCESSO-N

              MOVE 'ERRO DE FECHAMENTO DO ARQUIVO CLINEW'
                                           TO WS-MSG
              MOVE FS-CLINEW               TO WS-FS
              GO                           TO 9999-ERRO
           END-IF
           ACCEPT WS-HORARIO-FINAL FROM TIME

      *VARIAVEIS DE CALCULO DE TEMPO VINDAS DA BOOKLIB

           COPY CALCTEMP.

           MOVE WS-CTLIDO-CLIOLD           TO WS-CTLIDO-CLIOLD-F
           MOVE WS-CTLIDO-CLIMOV           TO WS-CTLIDO-CLIMOV-F
           MOVE WS-CTGRAV-CLINEW           TO WS-CTGRAV-CLINEW-F
           MOVE WS-CTINC                   TO WS-CTINC-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTALT                   TO WS-CTALT-F
           MOVE WS-CTCADINV                TO WS-CTCADINV-F
           MOVE WS-CTMOVINV                TO WS-CTMOVINV-F


           DISPLAY '=================================================='
           DISPLAY 'TOTAL DE CADASTROS LIDOS EM CLIOLD..........: '
                    WS-CTLIDO-CLIOLD-F
           DISPLAY 'TOTAL DE MOVIMENTOS LIDOS EM CLIMOV.........: '
                    WS-CTLIDO-CLIMOV-F
           DISPLAY 'TOTAL DE CADASTROS GRAVADOS EM CLINEW.......: '
                    WS-CTGRAV-CLINEW-F
           DISPLAY 'TOTAL DE CADASTROS INCLUIDOS EM CLINEW......: '
                    WS-CTINC-F
           DISPLAY 'TOTAL DE CADASTROS EXCLUIDOS EM CLIOLD......: '
                    WS-CTEXC-F
           DISPLAY 'TOTAL DE CADASTROS ALTERADOS PARA CLINEW....: '
                    WS-CTALT-F
           DISPLAY 'TOTAL DE CADASTROS INVALIDOS................: '
                    WS-CTCADINV-F
           DISPLAY 'TOTAL DE MOVIMENTOS INVALIDOS...............: '
                    WS-CTMOVINV-F
           DISPLAY 'TEMPO TOTAL DE PROCESSAMENTO.........: '
                    WS-TEMPO-PROCESSAMENTO-F
           DISPLAY '=================================================='
           DISPLAY '  TERMINO NORMAL DE PROCESSAMENTO GPXXNN06        '
           DISPLAY '=================================================='
           .

      *VARIAVEIS DE ROTINA DE DATA VINDAS DA BOOKLIB

           COPY ROTEDATA.

      *VARIAVEIS DE ROTINA DE ERRO VINDAS DA BOOKLIB

           COPY ROTERRO.
